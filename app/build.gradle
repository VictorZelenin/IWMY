import groovy.swing.SwingBuilder

import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':app:assembleGeneralRelease')) {
        def password = ''
        def hint = "Enter keystore/key password"
        if (System.console() != null) {
            password = new String(System.console().readPassword("\n\$ " + hint + ": "))
        } else {
            new SwingBuilder().edt {
                dialog(modal: true, alwaysOnTop: true, locationRelativeTo: null, pack: true,
                        show: true) {
                    vbox {
                        label(text: hint)
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            password = new String("" + input.password)
                            dispose()
                        })
                    }
                }
            }
        }
        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    def buildTimeMillis = System.currentTimeMillis()
    def buildTimeMinute = (int) (buildTimeMillis / 1000 / 60)
    def buildDateTime = new SimpleDateFormat("yyyy.MM.dd-HH.mm").format(buildTimeMillis)

    signingConfigs {
        release {
            storeFile file("IWMY-release.keystore.jks")
            storePassword "" // will be prompted
            keyAlias "IWMY-release"
            keyPassword "" // will be prompted
        }
    }

    defaultConfig {
        applicationId "com.oleksiykovtun.iwmy.speeddating"
        buildConfigField "String", "BACKEND_URL", "\"https://iwmy-speed-dating.appspot.com\""
        minSdkVersion 14
        targetSdkVersion 22
        versionCode buildTimeMinute
        versionName buildDateTime
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(output.outputFile.parent,
                        "IWMY-Speed-Dating" + "-" + variant.versionName + ".apk")
            }
        }

        debug {
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            proguardFile 'proguard-rules-release.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }

    productFlavors {
        developer {
            applicationId "com.oleksiykovtun.iwmy.speeddating.developer"
            buildConfigField "String",
                    "BACKEND_URL", "\"http://iwmy-speed-dating.oleksiykovtun.com:8082\""
            versionName buildDateTime + "-developer"
        }
        general {
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.6'
    compile project(':shared')
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
}
