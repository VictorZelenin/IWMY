import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    def version = new SimpleDateFormat("yyyy.MM.dd-HH.mm").format(System.currentTimeMillis())

    defaultConfig {
        applicationId "com.oleksiykovtun.iwmy.speeddating"
        buildConfigField "String", "BACKEND_URL", "\"http://iwmy-speed-dating.appspot.com\""
        resValue "color", "accent_color", "#0098FF"
        resValue "color", "main_color", "#A0D9FF"
        resValue "string", "app_name", "IWMY Speed Dating"
        resValue "string", "app_title", "Speed Dating"
        minSdkVersion 14
        targetSdkVersion 21
        versionName version
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(output.outputFile.parent,
                        "IWMY-Speed-Dating" + "-" + variant.versionName + ".apk")
            }
        }

        debug {
            versionNameSuffix "-debug"
        }
    }

    productFlavors {
        developer {
            applicationId "com.oleksiykovtun.iwmy.speeddating.developer"
            buildConfigField "String",
                    "BACKEND_URL", "\"http://iwmy-speed-dating.oleksiykovtun.com:8082\""
            resValue "color", "accent_color", "#FF7755"
            resValue "color", "main_color", "#FFCCAA"
            resValue "string", "app_name", "IWMY Speed Dating Developer"
            resValue "string", "app_title", "Speed Dating Developer"
            versionName version + "-developer"
        }
        general {
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
    compile project(':shared')
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
}
